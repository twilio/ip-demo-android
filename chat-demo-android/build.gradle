apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

def ACCESS_TOKEN_SERVICE_URL = project.hasProperty('ACCESS_TOKEN_SERVICE_URL') ? "\""+project.getProperty('ACCESS_TOKEN_SERVICE_URL')+"\"" : "\"http://example.com/token\""

dependencies {
    implementation "androidx.appcompat:appcompat:$androidXVersion"
    implementation 'com.google.android.material:material:1.0.0'

    // FCM
    // Firebase now requires the app gradle file to explicitly list
    // `com.google.firebase:firebase-core` as a dependency for Firebase services to work as expected.
    implementation 'com.google.firebase:firebase-core:16.0.9'
    implementation 'com.google.firebase:firebase-iid:18.0.0'
    implementation 'com.google.firebase:firebase-messaging:18.0.0'

    implementation "com.jakewharton:kotterknife:0.1.0-SNAPSHOT"
    implementation "eu.inloop:simplerecycleradapter:0.3.4"

    implementation 'com.twilio:chat-android-with-symbols:4.2.7'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
    implementation "org.jetbrains.anko:anko:0.10.5"

    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.crashlytics.sdk.android:crashlytics-ndk:2.1.0'
}

crashlytics {
    enableNdk true

    println "androidNdkOut \"$projectDir/build/intermediates/merged_native_libs/debug/out/lib\""
    androidNdkOut "$projectDir/build/intermediates/merged_native_libs/debug/out/lib"

    println "androidNdkLibsOut \"$projectDir/build/intermediates/stripped_native_libs/debug/out/lib\""
    androidNdkLibsOut "$projectDir/build/intermediates/stripped_native_libs/debug/out/lib"
}

android {
    compileSdkVersion androidCompileSdkVersion

    defaultConfig {
        applicationId "com.twilio.chat.demo"
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidCompileSdkVersion
        buildToolsVersion androidBuildToolsVersion

        versionCode 26
        versionName "1.0"

        buildConfigField "String", "ACCESS_TOKEN_SERVICE_URL", ACCESS_TOKEN_SERVICE_URL
        manifestPlaceholders = [fabricApiKey: project.findProperty("fabricApiKey") ?: ""]
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

     dexOptions {
        javaMaxHeapSize "4g"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

// Keep an eye on SDK deprecations
tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xdiags:verbose"
}

if (hasProperty("googleServicesJson")) {
    copy {
        from(googleServicesJson)
        into(projectDir)
    }
}

apply plugin: 'com.google.gms.google-services'
